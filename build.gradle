buildscript {
    repositories {
        google()
        mavenCentral()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:8.13.0'
    }
}

apply plugin: 'com.android.library'
apply plugin: 'maven-publish'

android {
    namespace = 'com.shockwave.pdfium'
    compileSdkVersion 36

    defaultConfig {
        minSdkVersion 23
        targetSdkVersion 36
        versionCode 3
        versionName "2.0.2"
        buildConfigField "String", "VERSION_NAME", "\"${versionName}\""

        externalNativeBuild {
            cmake {
                cppFlags '-Wall', '-Wextra', '-DHAVE_PTHREADS'
                arguments '-DANDROID_STL=c++_shared',
                          '-DANDROID_SUPPORT_FLEXIBLE_PAGE_SIZES=ON',
                          '-DANDROID_PLATFORM=android-26'
                abiFilters 'armeabi-v7a', 'arm64-v8a', 'x86', 'x86_64'
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }

    sourceSets {
        main {
            // Avoid packaging prebuilt libs from src/main/jni/lib as jniLibs.
            // Those libs are already provided via CMake IMPORTED targets.
            jni.srcDirs = []
            jniLibs.srcDirs = []
        }
    }

    externalNativeBuild {
        cmake {
            path file('CMakeLists.txt')
            version '3.22.1'
        }
    }

    buildFeatures {
        viewBinding = true
        buildConfig = true
    }

    ndkVersion = '28.2.13676358'

    packagingOptions {
        jniLibs {
            useLegacyPackaging = true
        }
    }

    // Configure AAR filename to include version
    libraryVariants.configureEach { variant ->
        variant.outputs.all {
            outputFileName = "PdfiumAndroid-${defaultConfig.versionName}-${variant.buildType.name}.aar"
        }
    }

    publishing {
        // Configura la pubblicazione per la variante 'release'
        singleVariant('release') {}
    }
}

repositories {
    google()
    mavenCentral()
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation 'androidx.core:core:1.17.0'
    androidTestImplementation 'androidx.test.ext:junit:1.3.0'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.7.0'
}
